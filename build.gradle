plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id "com.jfrog.bintray" version "1.0"
}

assert gradle.gradleVersion == '2.14'

sourceCompatibility = '1.8'

group = 'io.pivotal.labs'
version = '0.0.1'

description = file('README.adoc').readLines()[0]
ext.vendor = 'Pivotal Labs'
ext.url = 'https://github.com/pivotal/cf-env'
ext.license = 'BSD 2-Clause'

apply from: 'release.gradle'

configurations {
    doclet
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'

    doclet group: 'org.asciidoctor', name: 'asciidoclet', version: '1.5.4'
}

javadoc.options {
    docletpath = configurations.doclet.files.asType(List)
    doclet = 'org.asciidoctor.Asciidoclet'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.vendor,
                'Implementation-Vendor-Id': project.group,
                'Implementation-URL': project.url
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
    archives javadocJar
}

idea.module {
    outputDir = file('.idea/classes/main')
    testOutputDir = file('.idea/classes/test')
}
